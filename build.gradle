apply plugin: 'java'
apply plugin: 'eclipse'

buildscript {
	ext {
		appengineVersion = '1.7.1'
		appengineSdkBaseDir = new File(projectDir, '..')
		appengineSdkDir = new File(ext.appengineSdkBaseDir, "appengine-java-sdk-${ext.appengineVersion}")
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath group: 'com.google.appengine', name: 'appengine-tools-sdk', version: ext.appengineVersion 
	}
}

sourceCompatibility = 1.6

sourceSets {
	apt
	tools
}

configurations {
	appengineSdk
}

repositories {
	mavenCentral()
	mavenRepo(url: 'http://maven.seasar.org/maven2/')
	mavenRepo(url: 'http://mavenrepo.google-api-java-client.googlecode.com/hg')
}

dependencies {
	compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'

	compile group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: ext.appengineVersion
	compile group: 'com.google.appengine', name: 'appengine-api-labs', version: ext.appengineVersion
	testCompile group: 'com.google.appengine', name: 'appengine-testing', version: ext.appengineVersion
	testCompile group: 'com.google.appengine', name: 'appengine-api-stubs', version: ext.appengineVersion
	runtime group: 'com.google.appengine', name: 'appengine-tools-sdk', version: ext.appengineVersion

	compile(group: 'org.slim3', name: 'slim3', version: '1.0.16') { exclude group: 'com.google.appengine' }
	aptCompile(group: 'org.slim3', name: 'slim3-gen', version: '1.0.16') { exclude group: 'org.apache.ant' }

	testCompile group: 'junit', name: 'junit', version: '4.10'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.0'

	compile group: 'com.google.apis', name: 'google-api-services-tasks', version: 'v1-rev3-1.7.2-beta'
	compile group: 'com.google.http-client', name: 'google-http-client-appengine', version: '1.10.3-beta'
	compile group: 'commons-io', name: 'commons-io', version: '2.4'

	toolsCompile group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.7'
	toolsCompile group: 'com.googlecode.htmlcompressor', name: 'htmlcompressor', version: '1.5.2'

	appengineSdk group: 'com.google.appengine', name: 'appengine-java-sdk', version: ext.appengineVersion
}

/*
 * APT generation support 
 */
task compileAptJava(overwrite: true) {
	inputs.dir sourceSets.main.java.srcDirs
	outputs.dir sourceSets.apt.output.resourcesDir
	doLast {
		sourceSets.apt.output.resourcesDir.mkdirs()
		ant.path(id: 'aptFactoryPath', location: configurations.aptCompile.asPath)
		ant.apt(compile: false,
			includeAntRuntime: false,
			factorypathref: 'aptFactoryPath',
			classpath: configurations.compile.asPath,
			preprocessdir: sourceSets.apt.output.resourcesDir,
			encoding: 'UTF-8') {
			sourceSets.main.java.srcDirs.each{
				src(path: it)
			}
		}
	}
}

compileJava.dependsOn compileAptJava
compileJava.source sourceSets.apt.output.resourcesDir

/*
 * Eclipse support
 */
eclipse {
	classpath {
		plusConfigurations += configurations.toolsCompile
	}
}

task eclipseAptFactory(type: Copy) {
	from configurations.aptCompile.files
	into new File(buildDir, 'eclipse/apt-factory')
}

task cleanEclipseWebInfLib(type: Delete) {
	delete new File(projectDir, 'webapp/WEB-INF/lib')
}

task eclipseWebInfLib(type: Copy) {
	dependsOn cleanEclipseWebInfLib
	from configurations.runtime.files
	into new File(projectDir, 'webapp/WEB-INF/lib')
}

// overwrite to prevent breaking Eclipse project files
task eclipse(overwrite: true) {
	dependsOn eclipseAptFactory
	dependsOn eclipseWebInfLib
}

/*
 * deployment support
 */
task prepareWebapp {
	// FIXME
}

task appenginePrepareSdk {
	outputs.dir project.ext.appengineSdkDir
	doLast {
		copy {
			from zipTree(configurations.appengineSdk.singleFile)
			into ext.appengineSdkDir
		}
	}
}

task deploy {
	dependsOn appenginePrepareSdk
	// TODO: prepare webapp dir
	doLast {
		System.setProperty 'appengine.sdk.root', project.ext.appengineSdkDir.path
		String[] args = ['help']
		com.google.appengine.tools.admin.AppCfg.main args
	}
}

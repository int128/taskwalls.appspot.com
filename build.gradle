buildscript {
	ext {
		appengineVersion = '1.7.1'
	}
}

apply plugin: 'java'

sourceCompatibility = 1.6

ext {
	sourceWebappDir = file('webapp')
}

configurations {
	aptFactory
}

repositories {
	mavenCentral()
	maven { url 'http://maven.seasar.org/maven2/' }
	maven { url 'http://mavenrepo.google-api-java-client.googlecode.com/hg' }
}

dependencies {
	compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'

	compile group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: ext.appengineVersion
	compile group: 'com.google.appengine', name: 'appengine-api-labs', version: ext.appengineVersion
	compile(group: 'org.slim3', name: 'slim3', version: '1.0.16') { exclude group: 'com.google.appengine' }

	testCompile group: 'junit', name: 'junit', version: '4.10'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.0'
	testCompile group: 'com.google.appengine', name: 'appengine-testing', version: ext.appengineVersion
	testCompile group: 'com.google.appengine', name: 'appengine-api-stubs', version: ext.appengineVersion

	compile group: 'com.google.apis', name: 'google-api-services-tasks', version: 'v1-rev3-1.7.2-beta'
	compile group: 'com.google.http-client', name: 'google-http-client-appengine', version: '1.10.3-beta'
	compile group: 'commons-io', name: 'commons-io', version: '2.4'

	runtime group: 'com.google.appengine', name: 'appengine-tools-sdk', version: ext.appengineVersion

	aptFactory(group: 'org.slim3', name: 'slim3-gen', version: '1.0.16') { exclude group: 'org.apache.ant' }
}

apply from: 'deploy.gradle'

/*
 * APT generation support 
 */
ext {
	aptGeneratedDir = new File(buildDir, 'apt-generated')
}

task generateApt {
	description 'Generates APT sources.'
	inputs.dir sourceSets.main.java.srcDirs
	outputs.dir project.ext.aptGeneratedDir
	doLast {
		project.ext.aptGeneratedDir.mkdirs()
		ant.path(id: 'aptFactoryPath', location: configurations.aptFactory.asPath)
		ant.apt(compile: false,
			includeAntRuntime: false,
			factorypathref: 'aptFactoryPath',
			classpath: configurations.compile.asPath,
			preprocessdir: project.ext.aptGeneratedDir,
			encoding: 'UTF-8') {
			sourceSets.main.java.srcDirs.each{ src(path: it) }
		}
	}
}

compileJava {
	dependsOn generateApt
	source project.ext.aptGeneratedDir
}

/*
 * Eclipse support
 */
task eclipseAptFactory(type: Sync) {
	description 'Prepares APT factory for Eclipse.'
	from configurations.aptFactory.files
	into new File(buildDir, 'eclipse/apt-factory')
}

task eclipseWebInfLib(type: Sync) {
	description 'Prepares libraries to run DevAppServer.'
	from configurations.runtime.files
	into new File(project.ext.sourceWebappDir, 'WEB-INF/lib')
}
